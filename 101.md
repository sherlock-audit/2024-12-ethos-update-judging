Docile Lilac Gecko

Medium

# Incorrect Calculation of Price Per Vote in Sell Transactions

### Summary

The `sellVotes` function in the `ReputationMarket` contract is vulnerable to an issue in which the calculated `pricePerVote` includes protocol fees in the numerator. As a result, the function does not guarantee that the user receives the minimum expected value (`minimumVotePrice`) per vote after protocol fees are deducted. This could lead to unfair transactions, where users receive less than their specified minimum price per vote.
Line of code: [sellvotes()](https://github.com/sherlock-audit/2024-12-ethos-update/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L539-L578)
```solidity
  function sellVotes(
    //....//
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    //....

    //@audit the user is actually selling at price per token + fees. to maintain fairness, we should use price per token without fees(proceedsAfterFees).
    uint256 pricePerVote = votesToSell > 0 ? proceedsBeforeFees / votesToSell : 0;
    if (pricePerVote < minimumVotePrice) {
      revert SellSlippageLimitExceeded(minimumVotePrice, pricePerVote);
    }

    //....
  }
  ```
  

### Root Cause

The function calculates `pricePerVote` using the `proceedsBeforeFees` value, which includes protocol fees. The calculation should instead use `proceedsAfterFees` to ensure the actual amount received per vote meets or exceeds the `minimumVotePrice`.

### Impact

Users may receive less than their expected minimum price when selling votes due to the incorrect price calculation, leading to unexpected financial losses and eroding trust in the platformâ€™s fairness.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {ReputationMarket} from "../src/ReputationMarket.sol";
import {ContractAddressManager} from "../src/utils/ContractAddressManager.sol";
import {EthosProfile} from "../src/EthosProfile.sol";
import {Proxy} from "@openzeppelin/contracts/proxy/Proxy.sol";

contract ProxyTest is Proxy {
    address implementation;
    constructor(address implementation_) {
        implementation = implementation_;
    }
    function _implementation() internal view override returns (address) {
        return implementation;
    }
}

contract ReputationMarketExploitTest is Test {
    ContractAddressManager contractAddressManager;
    ProxyTest proxyReputationMarket;
    ProxyTest proxyEthosProfile;
    
    address owner = makeAddr("owner");
    address admin = makeAddr("admin");
    address signatureVerifier = makeAddr("signatureVerifier");
    address marketCreator = makeAddr("marketCreator");
    
    // Attack addresses
    address trustBuyingUser = makeAddr("trustBuyingUser");
    address untrustBuyingUser = makeAddr("untrustBuyingUser");
    address attacker = makeAddr("attacker");
    
    uint256 constant MARKET_ID = 2;
    uint256 constant LIQUIDITY_PARAMETER = 1000;
    uint256 constant BASE_PRICE = 0.01 ether;

    function setUp() public {
        console2.log("=== Setting up Test Environment ===");
        
        vm.startPrank(owner);
        
        // Deploy contracts
        ReputationMarket market = new ReputationMarket();
        EthosProfile ethosProfile = new EthosProfile();
        contractAddressManager = new ContractAddressManager();
        proxyReputationMarket = new ProxyTest(address(market));
        proxyEthosProfile = new ProxyTest(address(ethosProfile));

        // Initialize Market
        (bool success, ) = address(proxyReputationMarket).call(
            abi.encodeWithSignature(
                "initialize(address,address,address,address,address)", 
                owner, 
                admin, 
                address(market), 
                signatureVerifier, 
                address(contractAddressManager)
            )
        );
        require(success, "Market initialization failed");

        // Initialize Profile
        (success, ) = address(proxyEthosProfile).call(
            abi.encodeWithSignature(
                "initialize(address,address,address,address,address)", 
                owner, 
                admin, 
                address(ethosProfile), 
                signatureVerifier, 
                address(contractAddressManager)
            )
        );
        require(success, "Profile initialization failed");

        // Setup contract addresses
        address[] memory contractAddresses = new address[](1);
        string[] memory names = new string[](1);
        contractAddresses[0] = address(proxyEthosProfile);
        names[0] = "ETHOS_PROFILE";
        contractAddressManager.updateContractAddressesForNames(contractAddresses, names);

        // Invite market creator
        (success, ) = address(proxyEthosProfile).call(
            abi.encodeWithSignature("inviteAddress(address)", marketCreator)
        );
        vm.stopPrank();

        // Create profile for market creator
        vm.startPrank(marketCreator);
        (success, ) = address(proxyEthosProfile).call(
            abi.encodeWithSignature("createProfile(uint256)", 1)
        );
        vm.stopPrank();

        // Allow market creation
        vm.startPrank(admin);
        (success, ) = address(proxyReputationMarket).call(
            abi.encodeWithSignature("setUserAllowedToCreateMarket(uint256,bool)", MARKET_ID, true)
        );
        (success, ) = address(proxyReputationMarket).call(
            abi.encodeWithSignature("setProtocolFeeAddress(address)", owner)
        );
        (success, ) = address(proxyReputationMarket).call(
            abi.encodeWithSignature("setExitProtocolFeeBasisPoints(uint256)", 500)
        );
        vm.stopPrank();

        // Create market
        vm.startPrank(marketCreator);
        deal(marketCreator, 0.2 ether);
        (success, ) = address(proxyReputationMarket).call{value: 0.2 ether}(
            abi.encodeWithSignature("createMarket()")
        );
        vm.stopPrank();

        console2.log("Market created with:");
        console2.log("- Liquidity Parameter:", LIQUIDITY_PARAMETER);
        console2.log("- Base Price:", BASE_PRICE);
        console2.log("- Initial Balance:", address(proxyReputationMarket).balance);
    }

    function test_spillageWhileSelling() public {
        //1. Making a balanced market
        console2.log("\n=== Starting Exploit Test ===");
        vm.prank(trustBuyingUser);
        deal(trustBuyingUser, 1 ether);
        (bool success, bytes memory data) = address(proxyReputationMarket).call{value: 1 ether}(
            abi.encodeWithSignature("buyVotes(uint256,bool,uint256,uint256)", MARKET_ID, true, 1000, 0)
        );
        vm.prank(untrustBuyingUser);
        deal(untrustBuyingUser, 1 ether);
        (success, data) = address(proxyReputationMarket).call{value: 1 ether}(
            abi.encodeWithSignature("buyVotes(uint256,bool,uint256,uint256)", MARKET_ID, false, 1000, 0)
        );
        (, data) = address(proxyReputationMarket).call(
            abi.encodeWithSignature("getMarket(uint256)", MARKET_ID)
        );
        (ReputationMarket.MarketInfo memory marketInfo) = abi.decode(data, (ReputationMarket.MarketInfo));
        console2.log("\nMarket state after balanced votes:");
        console2.log("- Trust Votes:", marketInfo.trustVotes);
        console2.log("- Distrust Votes:", marketInfo.distrustVotes);

        //2. Selling trust votes
        vm.startPrank(trustBuyingUser);

        uint256 balanceBefore = address(trustBuyingUser).balance;
        (success, ) = address(proxyReputationMarket).call(
            abi.encodeWithSignature("sellVotes(uint256,bool,uint256,uint256)", MARKET_ID, true, 100, 4.8e15)
        );
        uint256 netBalance = address(trustBuyingUser).balance - balanceBefore;
        console2.log("\nActual PricePerToken from the trade:", netBalance/100);
        assertLt(netBalance/100, 4.8e15);
    }
}
```
Output of this test:
```solidity
  === Setting up Test Environment ===
  Market created with:
  - Liquidity Parameter: 1000
  - Base Price: 10000000000000000
  - Initial Balance: 200000000000000000
  
=== Starting Exploit Test ===
  
Market state after balanced votes:
  - Trust Votes: 191
  - Distrust Votes: 209
  
Expected PricePerToken from the trade: 4800000000000000
  
Actual PricePerToken from the trade: 4588595788213988
```

### Mitigation

Update the calculation of `pricePerVote` to use `proceedsAfterFees` instead of `proceedsBeforeFees`. This ensures that the user receives at least their specified `minimumVotePrice` after protocol fees are:
```solidity
uint256 pricePerVote = votesToSell > 0 ? proceedsAfterFees / votesToSell : 0;
if (pricePerVote < minimumVotePrice) {
  revert SellSlippageLimitExceeded(minimumVotePrice, pricePerVote);
}
```