Lucky Spruce Kookaburra

Medium

# Summation of Prices for Positive and Negative Votes is not always equal to 1, which breaks the invariant

### Summary

The summation of prices for positive and negative votes is not always equal to 1, which breaks the invariant.

> Must maintain LMSR invariant (yes + no price sum to 1)

### Root Cause

In [ReputationMarket.sol](https://github.com/sherlock-audit/2024-12-ethos-update/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L992C1-L1004C4), we round the number to ceil if it's negative and we round it to floor if it's positive. We do that in order to protect the invariant. But this is not enough for edge cases. 

 In edge case scenarios, we see that summation can be lower than 1. We will use fuzz test for reproducing the scenario. This scenario happens when one of the `odds` is divisible by `1e18` in `mulDiv`. `mulDiv` doesn't round it to ceil if it's already divisible. 
 
 In real world, if one side is divisible we expect that the other side is divisible too. But this is not the case in edgecases in solidity. One side can divisible to `1e18` while the other side is not and if divisible side is a negative vote then positive vote will be rounded to floor and negative part is also rounded to floor too.

### Internal Pre-conditions

1. Negative side is divisible to `1e18` but positive side is not.

### Impact

Medium - It breaks the main invariant of the protocol. The result of summation should be always equal to base price according to the invariant. The price calculation while `calculateSell` and `calculateBuy` will be wrong for 1 wei.

> Must maintain LMSR invariant (yes + no price sum to 1)

This issue will cause wrong accounting in following state variable

```solidity
    lastMarketUpdates[profileId] = MarketUpdateInfo({
      voteTrust: markets[profileId].votes[TRUST],
      voteDistrust: markets[profileId].votes[DISTRUST],
      positivePrice: currentPositivePrice,
      negativePrice: currentNegativePrice,
      lastUpdateBlock: block.number
    });
```

Mitigation should be applied for both rounding ( getPrice, calculateBuy, calculateSell )


### PoC

I saw that problem while the fuzz test in foundry. You can use following files to reproduce the bug.

First of all install the required libraries:

```console
forge init --force --no-commit
forge install PaulRBerg/prb-math@v4.1.0 openzeppelin/openzeppelin-contracts@v5.1.0 openzeppelin@openzeppelin-contracts-upgradeable@v5.1.0 --no-commit
```

Use following command for fuzz test
```console
forge test --match-test testFuzz_price -vvv
```

Use following command for example scenario
```console
forge test --match-test test_priceNotOne -vvv
```

```solidity
// ProfileMock.sol in mocks folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

/*
 * @dev Interface for EthosProfile Smart Contract.
 */
interface IEthosProfile {
  function verifiedProfileIdForAddress(address _address) external view returns (uint256);
}


contract ProfileMock is IEthosProfile{

    function verifiedProfileIdForAddress(address usr) external view returns(uint256){
        return uint256(keccak256(abi.encode(usr)));
    }
}
```
```solidity
// ManagerMock.sol in mocks folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

interface IContractAddressManager {
  function getContractAddressForName(string memory name) external view returns (address);

  function checkIsEthosContract(address targetAddress) external view returns (bool);
}

contract ManagerMock is IContractAddressManager{

    address profile;
    constructor(address _profile){
        profile = _profile;
    }
    function getContractAddressForName(string memory name) external view returns (address){
        return profile;
    }
    function checkIsEthosContract(address targetAddress) external view returns (bool){
        return true;
    }

}
```

```solidity
// ReputationTest.t.sol in test folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import "forge-std/Test.sol";
import "forge-std/console2.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import "../contracts/mocks/ProfileMock.sol";
import "../contracts/mocks/ManagerMock.sol";
import {ReputationMarket} from "../contracts/ReputationMarket.sol";


interface IReputation {
    function createMarket() external payable;
    function buyVotes(
        uint256 profileId,
        bool isPositive,
        uint256 maxVotesToBuy,
        uint256 minVotesToBuy
    ) external payable;
    function sellVotes(
        uint256 profileId,
        bool isPositive,
        uint256 votesToSell,
        uint256 minimumVotePrice
    ) external;
    function setAllowListEnforcement(bool) external;
    function setEntryProtocolFeeBasisPoints(uint256) external;
    function setDonationBasisPoints(uint256) external;
    function setProtocolFeeAddress(address) external;
    function withdrawDonations() external;
    function createMarketWithConfig(uint256) external payable;
    function getVotePrice(uint256,bool) external view returns(uint256);
}

contract ReputationUnitTest is Test {

    ProfileMock profile;
    ManagerMock manager;
    address owner = address(0x1);
    address admin = address(0x2);
    address alice = address(0x3);
    address bob = address(0x4);
    address john = address(0x5);
    address lucy = address(0x6);
    address proxy;
    uint256 profileIdAlice = uint256(keccak256(abi.encode(alice)));

    function setUp() public {
        address reputation = address(new ReputationMarket());
        profile = new ProfileMock();
        manager = new ManagerMock(address(profile));
        bytes memory data = abi.encodePacked(bytes4(keccak256("initialize(address,address,address,address,address)")),abi.encode(owner,admin,address(0x10),address(0x11),address(manager)));
        proxy = address(new ERC1967Proxy(reputation, data));
        vm.startPrank(admin);
        IReputation(proxy).setAllowListEnforcement(false);
        IReputation(proxy).setProtocolFeeAddress(admin);
        IReputation(proxy).setDonationBasisPoints(500);
        IReputation(proxy).setEntryProtocolFeeBasisPoints(500);
        vm.stopPrank();
        vm.deal(alice, 1000000 ether);
        vm.deal(bob, 1e18 ether);
        vm.deal(john, 1000000 ether);
        vm.deal(lucy, 1000000 ether);
    }

    /// forge-config: default.fuzz.runs = 100000
    /// forge-config: default.fuzz.max_test_rejects = 100000
    function testFuzz_price(bool b, uint16 x, uint16 y) public {

        vm.assume(x != 0);
        vm.assume(y != 0);
        x = uint16(bound(x, 100, 20000));
        y = uint16(bound(y, 100, 20000));
        
        uint256 basePrice = 0.01 ether;
        
        vm.prank(alice);
        IReputation(proxy).createMarket{value: 2 ether}();

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, b, uint256(x), uint256(x));

        uint256 priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        uint256 priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA + priceB == basePrice);

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, !b, uint256(y), uint256(y));

        priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA + priceB == basePrice);

        vm.prank(bob);
        IReputation(proxy).sellVotes(profileIdAlice, b, uint256(x), 0);

        priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA + priceB == basePrice);

        vm.prank(bob);
        IReputation(proxy).sellVotes(profileIdAlice, !b, uint256(y), 0);

        priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA + priceB == basePrice);

    }
    function test_priceNotOne() public {
        vm.prank(alice);
        IReputation(proxy).createMarket{value: 2 ether}();

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, false, uint256(13154), uint256(13154));

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, true, uint256(12313), uint256(12313));

        uint256 priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        uint256 priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);

        console2.log(priceA);
        console2.log(priceB);
        console2.log(priceB + priceA);

    }
}
```

### Mitigation

Following code will solve the problem:

```diff
  function _calcVotePrice(Market memory market, bool isPositive) private pure returns (uint256) {
    // odds are in a ratio of N / 1e18
    uint256 odds = LMSR.getOdds(
      market.votes[TRUST],
      market.votes[DISTRUST],
      market.liquidityParameter,
      isPositive
    );
    // multiply odds by base price to get price; divide by 1e18 to get price in wei
    // round up for trust, down for distrust so that prices always equal basePrice

+   uint256 addOne = (odds * market.basePrice % 1e18 == 0) && !isPositive ? 1 : 0;
+   if(odds == 5e17){
+    addOne = 0;
+   }

-   return odds.mulDiv(market.basePrice, 1e18, isPositive ? Math.Rounding.Floor : Math.Rounding.Ceil)
+   return odds.mulDiv(market.basePrice, 1e18, isPositive ? Math.Rounding.Floor : Math.Rounding.Ceil) + addOne;
  }
```

There are two edgecases. First one is divisibility problem and second one occurs when the odds are equal to eachother ( both of them is equal to 5e17 ).

After this modification, you can try the fuzz test again. It will be solved. The mitigation for other parties such as `calculateBuy`, `calculateSell` should be applied respect to this mitigation.